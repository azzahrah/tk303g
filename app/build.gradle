apply plugin: 'com.android.application'

String signPropsName = "signing.properties"
boolean sign = (project.hasProperty(signPropsName) && new File(project.property(signPropsName)).exists())
Properties signProps = new Properties()

if (sign) {
    signProps.load(new FileInputStream(file(project.property(signPropsName))))
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "br.com.pilovieira.tk303g"
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        if (sign) {
            signConfig {
                keyAlias signProps['tk303g.keyAlias']
                keyPassword signProps['tk303g.keyPassword']
                storeFile file(signProps['tk303g.storeFile'])
                storePassword signProps['tk303g.storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (sign) {
                signingConfig signingConfigs.signConfig
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.j256.ormlite:ormlite-android:4.45'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.google.android.gms:play-services-ads:9.8.0'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
